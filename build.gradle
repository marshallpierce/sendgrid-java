/**
 * Commands:
 *   - gradle build
 *   - gradle test
 *   - gradle assemble
 *   - gradle uploadArchives
 *
 * To execute the 'uploadArchives' task, the following properties must be specified
 * in an external 'gradle.properties' file:
 *   - sonatypeUsername
 *   - sonatypePassword
 */

plugins {
  id 'java'
  id 'maven'
  id 'signing'
  id "com.github.johnrengelman.shadow" version "2.0.4"
}

group         = 'com.sendgrid'
version       = '4.2.1'
ext.packaging = 'jar'

allprojects {
  apply plugin: 'java'
  sourceCompatibility = 1.7
  targetCompatibility = 1.7
}

if (!hasProperty("sonatypeUsername")) {
  ext.sonatypeUsername = null
  ext.sonatypePassword = null
}

dependencies {
  compile 'com.sendgrid:java-http-client:4.1.0'
  compile 'com.fasterxml.jackson.core:jackson-core:2.9.5'
  compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.5'
  compile 'com.fasterxml.jackson.core:jackson-databind:2.9.5'
  testCompile group: 'junit', name: 'junit', version: '4.12'
}

repositories {
  mavenCentral()
}

allprojects {
  gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
      options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
      }
  }
}

shadowJar {
  manifest {
        attributes("Implementation-Title": "SendGrid", "Implementation-Version": version)
  }
}

// copy shadowJar to base project directory so they will be in git (and on github for download)
assemble.doLast {
  copy {
    from("$buildDir/libs/${shadowJar.archiveName}")
    into("$projectDir")
  }
}

task startPrism(type: Exec) {
  workingDir 'scripts'
  commandLine './startPrism.sh'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

signing {
  required { gradle.taskGraph.hasTask("uploadArchives") }
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
      }

      pom.project {
        name 'sendgrid-java'
        packaging 'jar'
        description 'SendGrid Java helper library'
        url 'https://github.com/sendgrid/sendgrid-java'

        scm {
          url 'scm:git@github.com:sendgrid/sendgrid-java.git'
          connection 'scm:git@github.com:sendgrid/sendgrid-java.git'
          developerConnection 'scm:git@github.com:sendgrid/sendgrid-java.git'
        }

        licenses {
          license {
            name 'MIT License'
            url 'http://opensource.org/licenses/MIT'
            distribution 'repo'
          }
        }

        developers {
          developer {
            id 'thinkingserious'
            name 'Elmer Thomas'
          }
          developer {
            id 'scottmotte'
            name 'Scott Motte'
          }
          developer {
            id 'elbou8'
            name 'Yamil Asusta'
          }
          developer {
            id 'eddiezane'
            name 'Eddie Zaneski'
          }
        }
      }
    }
  }
}

artifacts {
  archives shadowJar
  archives jar
  archives javadocJar
  archives sourcesJar
}
